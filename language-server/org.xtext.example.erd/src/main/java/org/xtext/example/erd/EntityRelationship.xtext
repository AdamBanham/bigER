grammar org.xtext.example.erd.EntityRelationship with org.eclipse.xtext.common.Terminals

generate entityRelationship "http://www.xtext.org/example/erd/EntityRelationship"

Model:
	'erdiagram' name=ID
	// option to automatically generate SQL code
	(generateSql='generateSql')?

	((entities+=Entity) | (relationships+=Relationship))*;

Entity:
	(weak?='weak')? 'entity' name=ID ('extends' extends=[Entity])? ('{'
		(attributes += Attribute)*
	'}')?;

Relationship:
	(weak?='weak')? 'relationship' name=ID ('{'
		(left=RelationEntity '->' right=RelationEntity)
		('->' third=RelationEntity)?
		(attributes += Attribute)*
	'}')?;

RelationEntity:
	target=[Entity] ('['
		((multiplicity=MultiplicityType) | (customMultiplicity=STRING))
	']')?;

Attribute:
	name=ID (':' datatype=DataType)? (type=AttributeType)?;

enum DataType:
	NONE = 'none' | INT = 'int' | DOUBLE = 'double' | STRING = 'string' | BOOLEAN = 'boolean' | DATETIME = 'datetime';
	
enum AttributeType:
	NONE = 'none' | KEY = 'key' | FOREIGN_KEY = 'foreign-key' | PARTIAL_KEY = 'partial-key' | NULLABLE = 'optional' | 
	DERIVED = 'derived' | MULTIVALUED = 'multivalued';

enum MultiplicityType:
	One = '1' | Many = 'N';