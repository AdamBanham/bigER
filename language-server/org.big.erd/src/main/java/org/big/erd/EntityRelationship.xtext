grammar org.big.erd.EntityRelationship with org.eclipse.xtext.common.Terminals

generate entityRelationship "http://www.big.org/erd/EntityRelationship"

Model:
	'erdiagram' name=ID
	generateOption=GenerateOption?
	notationOption=NotationOption?
	(entities+=Entity | relationships+=Relationship)*;

GenerateOption:
	'generate' '=' generateOptionType=GenerateOptionType;

NotationOption:
	'notation' '=' notationOptionType=NotationOptionType;

Entity:
	(weak?='weak')? 'entity' name=ID ('extends' extends=[Entity])? '{'
		(attributes += Attribute)*
	'}';

Relationship:
	(weak?='weak')? 'relationship' name=ID '{'
		(first=RelationEntity (('->' second=RelationEntity) ('->' third=RelationEntity)?)?)?
		(attributes += Attribute)*
	'}';

RelationEntity:
	target=[Entity] ('[' 
		(cardinality=CardinalityType | customMultiplicity=STRING)
	']')?;

Attribute:
	name=ID (':' datatype=DataType)? (type=AttributeType)?;

DataType:
	type=ID ('(' size=INT ')')?;  

enum AttributeType:
	NONE = 'none' | 
	KEY = 'key' | 
	PARTIAL_KEY = 'partial-key' | 
	OPTIONAL = 'optional' |  
	DERIVED = 'derived' | 
	MULTIVALUED = 'multivalued';

enum CardinalityType:
	NONE = 'none' |
	ONE = '1' | 
	MANY = 'N';

enum GenerateOptionType:
	OFF = 'off' | 
	SQL = 'sql';

enum NotationOptionType:
	DEFAULT = 'default';